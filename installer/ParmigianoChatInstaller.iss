; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppId "{{A4C5B89C-92D3-4AF9-BE3E-74B3D0C31402}}"
#define AppName "Parmigiano"
#define AppVersion "0.9.0"
#define AlphaVersion "2"
#define AppDisplayName "Parmigiano Chat v0.9.0"
#define AppPublisher "Parmigiano"
#define OutputDir "SetupOutput"
#define ResourcesDir "Public\assets"
#define AppIconName "D:\Projects\parmigiano\parmigiano-desktop\Public\assets\logo-ico.ico"
#define DesktopIconFile "D:\Projects\parmigiano\parmigiano-desktop\Public\assets\logo-x50.ico"
#define AppContact "parmigianochat@gmail.com"

[Setup]
SignTool=MsSign $f
AppId={#AppId}
AppName={#AppName}
AppVersion={#AppVersion}
AppPublisher={#AppPublisher}
DefaultDirName={pf}\Parmigiano
DefaultGroupName=Parmigiano
AllowNoIcons=yes
SetupIconFile={#AppIconName}
OutputDir={#OutputDir}
OutputBaseFilename=ParmigianoChatSetup-v{#AppVersion}-alpha.{#AlphaVersion}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
PrivilegesRequired=admin
ArchitecturesInstallIn64BitMode=x64compatible
UninstallDisplayIcon={app}\Parmigiano.exe
LicenseFile="../LICENSE"
AppCopyright=© 2025 Parmigiano
AppContact={#AppContact}

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "Создать ярлык на рабочем столе"; GroupDescription: "Дополнительно:"; Flags: unchecked
Name: "autostart"; Description: "Запускать вместе с Windows"; GroupDescription: "Дополнительно:"

[Files]
; Основное приложение
Source: "D:\Projects\parmigiano\parmigiano-desktop\bin\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs


; Фоновый listener
Source: "D:\Projects\parmigiano\ParmigianoChatBroker\bin\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\Parmigiano Chat"; Filename: "{app}\Parmigiano.exe"; IconFilename: {#DesktopIconFile}; WorkingDir: "{app}"
Name: "{userdesktop}\Parmigiano Chat"; Filename: "{app}\Parmigiano.exe"; IconFilename: {#DesktopIconFile}; WorkingDir: "{app}"; Tasks: desktopicon

[Registry]
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "Parmigiano Desktop"; ValueData: """{app}\Parmigiano.exe"""; Tasks: autostart; Flags: uninsdeletevalue

[Run]
Filename: "{app}\Parmigiano.exe"; Description: "Запустить Parmigiano Chat"; Flags: nowait postinstall skipifsilent

[Code]
const
  EVENT_MODIFY_STATE = $0002;

function OpenEvent(dwDesiredAccess: Cardinal; bInheritHandle: LongBool; lpName: AnsiString): THandle; external 'OpenEventA@kernel32.dll stdcall';
function SetEvent(hEvent: THandle): LongBool; external 'SetEvent@kernel32.dll stdcall';
function CloseHandle(hObject: THandle): LongBool; external 'CloseHandle@kernel32.dll stdcall';
procedure Sleep(dwMilliseconds: Cardinal); external 'Sleep@kernel32.dll stdcall';

procedure SignalBrokerAndMaybeKill();
var
  h: THandle;
  ErrorCode: Integer;
begin
  h := OpenEvent(EVENT_MODIFY_STATE, False, 'Global\ParmigianoChatBroker_Stop');
  if h <> 0 then
  begin
    SetEvent(h);
    CloseHandle(h);

    Sleep(2000);
  end;

  Exec('taskkill', '/F /IM ParmigianoChatBroker.exe /T', '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);
end;

procedure CurUninstallStepChanged(CurStep: TUninstallStep);
begin
  if CurStep = usUninstall then
  begin
    SignalBrokerAndMaybeKill();
  end;
end;

[UninstallDelete]
Type: filesandordirs; Name: "{app}"